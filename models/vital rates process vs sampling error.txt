# Anna Moeller 
# 11/19/2021

# data structure for each vital rate:
# 1 row for each estimate 
# columns: estimate, tau, age (1-2), nest attempt (1-2)

model{

  # Survival - S[age]
  # Prior
  for(a in 1:nage){
    S[a] ~ dbeta(1, 1) # equivalent to uniform(0, 1)
  }
  # Likelihood
  for(i in 1:nS){
    sdat[i, 1] ~ dnorm(S[sdat[i, 3]], sdat[i, 2]) # beta or on logit scale 
  }
  
  # Nest initiation rate - NI[age, attempt]
  # Prior 
  for(a in 1:nage){
    for(attempt in 1:nattempt){
      NI[a, attempt] ~ dbeta(1, 1)
    }
  }
  # Likelihood 
  for(i in 1:nNI){
    nidat[i, 1] ~ dnorm(NI[nidat[i, 4], nidat[i, 3]], nidat[i, 2])
  }
  
  # Hatching rate - H[age, attempt]
  # Prior 
  for(a in 1:nage){
    for(attempt in 1:nhp){
      H[a, attempt] ~ dbeta(1, 1)
    }
  }
  # Likelihood 
  for(i in 1:nH){
    hdat[i, 1] ~ dnorm(H[hdat[i, 4], hdat[i, 3]], hdat[i, 2])
  }
  
  # Nest success - NS[age, attempt]
  # Prior 
  for(a in 1:nage){
    for(attempt in 1:nnsp){
      NS[a, attempt] ~ dbeta(1, 1)
    }
  }
  # Likelihood 
  for(i in 1:nNS){
    nsdat[i, 1] ~ dnorm(NS[nsdat[i, 4], nsdat[i, 3]], nsdat[i, 2])
  }
  
  # Clutch size - C[age, attempt]
  # Prior 
  for(a in 1:nage){
    for(attempt in 1:ncp){
      C[a, attempt] ~ dnorm(meanc, tauc)
    }
  }
  # Likelihood
  for(i in 1:nC){
    cdat[i, 1] ~ dnorm(C[cdat[i, 4], cdat[i, 3]], cdat[i, 2])
  }
  
  # Poult survival - PS[age] (we don't have attempt #)
  # Prior 
  for(a in 1:nage){
    PS[a] ~ dbeta(1, 1) 
  }
  # Likelihood
 for(i in 1:nPS){
    psdat[i, 1] ~ dnorm(PS[psdat[i, 4]], psdat[i, 2]) # 0-28 days only
  }

  # Prior 
#  for(a in 1:nage){
#    PSD[a] ~ dbeta(1, 1)
#  }
  # Likelihood
#  for(i in 1:nPS){
#    psdat[i, 1] ~ dnorm((PSD[psdat[i, 4]])^(psdat[i, 3]), psdat[i, 2])
#  }
  # Derived survival to 28 days 
#  for(a in 1:nage){
#    PS[a] <- (PSD[a])^28
#  }

  # Derive youth survival
  YS <- S[1]^(48/52)

  # Derived parameters 
  # Reproduction 
  # note we only have data for 1st nest hatching and that's hard-coded here
  for(a in 1:nage){
    R[a] <- NI[a, 1] * C[a, 1] * 0.5 * NS[a, 1] * H[a, 1] * PS[a] * S[1]^(48/52) + 
      NI[a, 1] * (1-NS[a, 1]) * NI[a, 2] * C[a, 2] * 0.5 * NS[a, 2] * H[a, 1] * PS[a] * S[1]^(48/52)
  }

}